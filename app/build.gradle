plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

// Generate timestamp-based version suffix: yyyy.MM.dd.HH.mm.ss
def buildTimestamp = LocalDateTime.now().format(DateTimeFormatter.ofPattern("yyyy.MM.dd.HH.mm.ss"))
def baseVersionName = "hk-v"
def baseVersionCode = 1042002

android {
    namespace 'org.pocketworkstation.pckeyboard'
    compileSdk 36

    defaultConfig {
        applicationId 'org.pocketworkstation.pckeyboard'
        minSdk 21
        targetSdk 36
        versionCode baseVersionCode
        versionName "${baseVersionName}${buildTimestamp}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }

    signingConfigs {
        release {
            storeFile = file("keystore.jks")
            storePassword = System.getenv("SIGNING_STORE_PASSWORD")
            keyAlias = System.getenv("SIGNING_KEY_ALIAS")
            keyPassword = System.getenv("SIGNING_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig = signingConfigs.release
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    lint {
        checkReleaseBuilds false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    implementation 'androidx.core:core-ktx:1.15.0'

    // AndroidX dependencies
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'

    // Guava for BiMap
    implementation 'com.google.guava:guava:33.3.1-android'

    // Unit testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.4.0'
    testImplementation 'org.robolectric:robolectric:4.14.1'

    // Instrumented testing (AndroidX Test)
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

// Task to print version name (used by CI/CD)
tasks.register('printVersionName') {
    doLast {
        println android.defaultConfig.versionName
    }
}

// Task to print version code
tasks.register('printVersionCode') {
    doLast {
        println android.defaultConfig.versionCode
    }
}

// Task to print build timestamp
tasks.register('printBuildTimestamp') {
    doLast {
        println buildTimestamp
    }
}
